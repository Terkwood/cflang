|width, height| { -- constants

  |mandelbrot, countup, lookup| {
    countup: 0, height - 1, 1, |y| {
      countup: 0, width - 1, 1, |x| {
        mandelbrot: x, y, |value| {
          lookup: value, |char| {
            char!!;
          };
        };
      };
      10!!;
    };
  }

  -- mandelbrot
  : |px, py, yield| {
    |max_iterations, x0, y0| {
      |aux| {
        aux: aux, 0, 0, 0;
      }
      : |aux, i, x, y| {
        -- for some reason if-call chaining didn't wanna work here :(
        ~(x*x + y*y <= 4 and i < max_iterations) : {
          yield: i / max_iterations;
        };
        (x*x + y*y <= 4 and i < max_iterations) : {
          aux:
            aux,
            i + 1,
            x*x - y*y + x0,
            2*x*y + y0;
        };
      };
    }
    : 200 -- max_iterations
    , px / width * 3.5 - 2.5 -- x0
    , py / height * 2 - 1    -- y0
    ;
  }

  -- countup
  , |min, max, stride, yield| {
    |aux| {
      aux: aux, min;
    }
    : |aux, i| {
      (i <= max) : {
        yield: i;
        aux: aux, i + stride;
      };
    };
  }

  -- lookup
  , |value, yield| {
    (value > 0.2) : {
      yield: 42; -- asterisk
    } : {
      yield: 32; -- space
    };
  }

  ;

}
: 80 -- width
, 24 -- height
